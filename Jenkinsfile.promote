@Library('jenkins-pipeline-shared-libraries')_

deployProperties = [:]

pipeline {
    agent {
        label 'kie-rhel7 && kie-mem16g'
    }
    environment {
        PROPERTIES_FILE_NAME = "deployment.properties"
        BOT_CREDENTIALS_ID = "bsig-gh-bot"
    }
    parameters {
        string(name: 'DISPLAY_NAME', defaultValue: '', description: 'Setup a specific build display name')
        string(name: 'DEPLOY_BUILD_URL', defaultValue: '', description: 'URL to jenkins deploy build to retrieve the `deployment.properties` file. If base parameters are defined, they will override the `deployment.properties` information')
        string(name: 'BUILD_BRANCH_NAME', defaultValue: 'master', description: 'Override `deployment.properties`. Which branch to build? Set if you are not on a multibranch pipeline.')
        booleanParam(name: 'RELEASE', defaultValue: false, description: 'Override `deployment.properties`. Is this build for a release?')
        string(name: 'PROJECT_VERSION', defaultValue: '', description: 'Override `deployment.properties`. Optional if not RELEASE. If RELEASE, cannot be empty.')
        string(name: 'GIT_TAG', defaultValue: '', description: 'Git tag to set, if different from PROJECT_VERSION')
    }
    stages {
        stage('Initialization') {
            steps {
                script {
                    if (params.DISPLAY_NAME != "") {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    readDeployProperties()

                    if (isRelease()) {
                        assert getProjectVersion() != ''
                    }
                    if (getGitTag() != ""){
                        assert getBuildBranch() != ''
                    }
                }
            }
        }
        stage('Is staging repository promoted?') {
            when {
                expression { return isRelease() && getDeployProperty('staging-repo.url') != '' }
            }
            steps {
                script {
                    def pipelineName = "Kogito Runtimes promote pipeline"
                    withCredentials([string(credentialsId: "KOGITO_CI_EMAIL_TO", variable: 'ZULIP_EMAIL')]) {
                        emailext body: "${pipelineName} #${env.BUILD_NUMBER} is ready for promotion.\n" +
                                 "The staging repository can be found at: ${getDeployProperty('staging-repo.url')}\n" +
                                 "Please confirm that the staging repository has been promoted here: ${env.BUILD_URL}input",
                                 subject: "${pipelineName} #${getBuildBranch()}",
                                 to: ZULIP_EMAIL
                    }
                    input message: "Has the staging repository been promoted?", ok: "Yes"
                }
            }
        }
        stage('Merge deploy PR') {
            when {
                expression {
                    return isRelease() && 
                           getDeployProperty('kogito-runtimes.pr.link') != '' && 
                           getDeployProperty('kogito-apps.pr.link') != '' && 
                           getDeployProperty('kogito-examples.pr.link') != ''
                }
            }
            steps{
                dir("kogito-runtimes") {
                    checkoutRepo("kogito-runtimes")
                    mergeAndPush("kogito-runtimes")
                }
                dir("kogito-apps") {
                    checkoutRepo("kogito-apps")
                    mergeAndPush("kogito-apps")
                }
                dir("kogito-examples") {
                    checkoutRepo("kogito-examples")
                    mergeAndPush("kogito-examples")
                }
            }
        }
        stage('Git tag') {
            when {
                expression { return getGitTag() != "" }
            }
            steps {
                dir("kogito-runtimes") {
                    tagLatest("kogito-runtimes")
                }
                dir("kogito-apps") {
                    tagLatest("kogito-apps")
                }
                dir("kogito-examples") {
                    tagLatest("kogito-examples")
                }
            }
        }
        stage('Prepare for snapshot PR'){
            when {
                expression { return isRelease() }
            }
            steps {
                dir("kogito-runtimes-bot") {
                    prepareForPR()
                }
                dir("kogito-apps-bot") {
                    prepareForPR()
                }
                dir("kogito-examples-bot") {
                    prepareForPR()
                }
            }
        }
        stage('Set next snapshot') {
            when {
                expression { return getProjectVersion() != "" }
            }
            steps {
                dir("kogito-runtimes-bot") {
                    maven.mvnVersionsSet(getSnapshotVersion())
                    sh "mvn -B clean install -DskipTests"
                }
                dir("kogito-apps-bot") {
                    maven.mvnVersionsUpdateParentAndChildModules(getSnapshotVersion())
                }
                dir("kogito-examples-bot") {
                    maven.mvnVersionsSet(getSnapshotVersion())
                }
            }
        }
        stage('Create snapshot PR'){
            when {
                expression { return isRelease() }
            }
            steps {
                dir("kogito-runtimes-bot") {
                    script {
                        commitAndCreatePR("kogito-runtimes")
                    }
                }
                dir("kogito-apps-bot") {
                    script {
                        commitAndCreatePR("kogito-apps")
                    }
                }
                dir("kogito-examples") {
                    script {
                        commitAndCreatePR("kogito-examples")
                    }
                }
            }
        }
        stage('Merge snapshot PR') {
            when {
                expression {
                    return isRelease() && 
                           getDeployProperty('kogito-runtimes.pr.link') != '' && 
                           getDeployProperty('kogito-apps.pr.link') != '' && 
                           getDeployProperty('kogito-examples.pr.link') != ''
                }
            }
            steps{
                dir("kogito-runtimes") {
                    mergeAndPush("kogito-runtimes")
                }
                dir("kogito-apps") {
                    mergeAndPush("kogito-apps")
                }
                dir("kogito-examples") {
                    mergeAndPush("kogito-examples")
                }
            }
        }
    }
}

//////////////////////////////////////////////////////////////////////////////
// Deployment properties
//////////////////////////////////////////////////////////////////////////////

void readDeployProperties(){
    if (params.DEPLOY_BUILD_URL != null){
        sh "wget ${params.DEPLOY_BUILD_URL}artifact/${PROPERTIES_FILE_NAME}"
        deployProperties = readProperties file: PROPERTIES_FILE_NAME
        // echo all properties
        echo deployProperties.collect{ entry -> "${entry.key}=${entry.value}" }.join("\n")
    }
}

boolean hasDeployProperty(String key){
    return deployProperties[key] != null
}

String getDeployProperty(String key){
    if(hasDeployProperty(key)){
        return deployProperties[key]
    }
    return ""
}

String getParamOrDeployProperty(String paramKey, String deployPropertyKey){
    if (params[paramKey] != ""){
        return params[paramKey]
    }
    return getDeployProperty(deployPropertyKey)
}

//////////////////////////////////////////////////////////////////////////////
// Getter / Setter
//////////////////////////////////////////////////////////////////////////////

boolean isRelease() {
    return params.RELEASE || (getDeployProperty("release") == "true")
}

String getProjectVersion() {
    return getParamOrDeployProperty("PROJECT_VERSION", "project.version")
}

String getSnapshotVersion() {
    return util.getNextSnapshot(getProjectVersion())
}

String getGitTag() {
    return params.GIT_TAG != "" ? params.GIT_TAG : getProjectVersion()
}

String getBuildBranch() {
    return getParamOrDeployProperty("BUILD_BRANCH_NAME", "git.branch")
}

//////////////////////////////////////////////////////////////////////////////
// Git
//////////////////////////////////////////////////////////////////////////////

void checkoutRepo(String repo) {
    checkout(githubscm.resolveRepository(repo, "kiegroup", getBuildBranch(), false))
    sh "git checkout ${getBuildBranch()}"
}

void mergeAndPush(String repo) {
    githubscm.mergePR(getDeployProperty("${repo}.pr.link"))
    githubscm.pushObject("origin", getBuildBranch())
}

void tagLatest(String repo) {
    githubscm.tagRepository(getGitTag(), BUILD_TAG)
    githubscm.pushObject("origin", BUILD_TAG)
}

void prepareForPR() {
    checkout(githubscm.resolveRepository(repo, env.BOT_CREDENTIALS_ID, getBuildBranch(), false))
    githubscm.forkRepo(env.BOT_CREDENTIALS_ID)
    githubscm.createBranch(getSnapshotVersion())
}

void addNotIgnoredPoms() {
    // based on https://stackoverflow.com/a/59888964/8811872
    sh '''
    find . -type f -name 'pom.xml' > found_poms.txt
    poms_to_add=""
    while IFS= read -r pom; do
        if ! git check-ignore -q "\$pom"; then
            poms_to_add="\$poms_to_add \$pom"
        fi
    done < found_poms.txt
    rm found_poms.txt
    git add \$poms_to_add
    '''
}

void commitAndCreatePR(String repo) {
    def commitMsg = "Update snapshot version to ${getSnapshotVersion()}"
    def prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}"
    // Not using githubscm.commitChanges() because globbing won't work.
    // See: https://github.com/kiegroup/kogito-runtimes/pull/570#discussion_r449268738
    addNotIgnoredPoms()
    sh "git commit -m '${commitMsg}'"
    githubscm.pushObject("origin", getSnapshotVersion(), env.BOT_CREDENTIALS_ID)
    deployProperties["${repo}.pr.link"] = githubscm.createPR(commitMsg, prBody, getBuildBranch(), env.BOT_CREDENTIALS_ID)
}

